; RA0 - 7セグ 左A
; RA1 - 7セグ 左B
; RA2 - 7セグ 左C
; RA3 - 7セグ 左D
; RB0 - 7セグ 右A
; RB1 - 7セグ 右B
; RB2 - 7セグ 右C
; RB3 - 7セグ 右D
; RB4 - PSW1
; RB5 - PSW2

	LIST P=PIC16F84A
	#INCLUDE <P16F84A.INC>

	__CONFIG _HS_OSC &_CP_OFF &_WDT_OFF &_PWRTE_ON

XORLF	MACRO	FILE, LIT
		MOVLW	LIT
		XORWF	FILE, W
		ENDM

JZ		MACRO	LABEL
		BTFSC	STATUS, 2
		GOTO	LABEL
		ENDM

MOVLF	MACRO	REG, IMM
		MOVLW	IMM
		MOVWF	REG
		ENDM

PLAYS	MACRO	_SCALE
		MOVLF	SCALE, _SCALE
		CALL	PLAY16
		ENDM

	CBLOCK	0CH
SEGL	; 0~9
SEGR	; 0~9
; 0 -> 動作状態（両方）
; 1 -> 動作状態（右）
; 2 -> 待機状態
LOCKFLG
W_TEMP
STATUS_TEMP
CNT1
CNT2
CNT3
CNT4
CNT5
CNT50U	;TIME50Uで使用される
CNT_	;TIME_で使用される
CNTx5	;PLAYx5で使用される
CNT16	;PLAY16で使用される
ARGPRE	;TIME_でのPREループの数
ARGPOST	;TIME_でのメインループの数
ARG16	;PLAY16でのループ数
SCALE	;音階(0:D4)
	ENDC

	CBLOCK 0H
D4
E4
FS4
G4
GS4
A4
AS4
B4
C5
CS5
D5
DS5
E5
F5
FS5
G5
GS5
A5
	ENDC

	ORG		0H
	GOTO	MAIN

	ORG		04H

; PUSH
	MOVWF	W_TEMP
	MOVF	STATUS, W
	MOVWF	STATUS_TEMP

	BTFSC	PORTB, 4
	GOTO	PSW1
	BTFSC	PORTB, 5
	GOTO	PSW2
	GOTO	_POP

; ストップボタンが押された場合の処理
PSW1
	XORLF	LOCKFLG, 2
	JZ		_POP

	CALL	ROTATE_LOCK

	XORLF	LOCKFLG, 1
	JZ		_POP
	XORLF	LOCKFLG, 2
	JZ		TO2

TO2
;JUDGE
	MOVF	SEGL, W
	XORWF	SEGR, W
	JZ		SUCCESS
FAILURE
;PLAY_FAIRURE
	PLAYS	CS5
	PLAYS	C5
	PLAYS	B4
	PLAYS	AS4
	PLAYS	A4
	
	GOTO	_POP

SUCCESS
;PLAY_SUCCESS
	PLAYS	A4
	PLAYS	AS4
	PLAYS	B4
	PLAYS	C5
	PLAYS	CS5
;FLASH
	MOVLW	0AH
	MOVWF	CNT5
FLASHLOOP
	MOVLW	B'11111111'
	MOVWF	PORTA
	MOVWF	PORTB
	CALL	TIME_L
	CALL	UPDATESEG
	CALL	TIME_L
	DECFSZ	CNT5, F
	GOTO	FLASHLOOP

	GOTO	_POP

; スタートボタンが押された場合の処理
PSW2
	XORLF	LOCKFLG, 2
	BTFSS	STATUS, 2
	GOTO	_POP

	CALL	ROTATE_LOCK

;PLAY_FANFARE
	PLAYS	CS5
	PLAYS	D5
	PLAYS	DS5
	PLAYS	E5

	PLAYS	DS5
	PLAYS	E5
	PLAYS	F5
	PLAYS	FS5

_POP
	MOVF	STATUS_TEMP, W
	MOVWF	STATUS
	SWAPF	W_TEMP, W

	BCF		INTCON, RBIF
	RETFIE

; LOCKFLGを次の状態にする。（0->1->2->0）
ROTATE_LOCK
	INCF	LOCKFLG, F
	XORLF	LOCKFLG, 3H
	BTFSC	STATUS, 2
	CLRF	LOCKFLG
	RETURN

; MAINサブルーチン
MAIN
	; 割り込み設定
	BCF		INTCON, GIE
	BSF		INTCON, RBIE

	; ピン入出力モード設定
	BSF		STATUS, RP0
	CLRF	TRISA
	MOVLW	B'00110000'
	MOVWF	TRISB
	BCF		STATUS, RP0

	; レジスタの初期化
	CLRF	PORTA
	CLRF	PORTB
	CLRF	SEGL
	CLRF	SEGR
	MOVLW	02H
	MOVWF	LOCKFLG

	; 割り込み設定
	BSF		INTCON, GIE
	BCF		INTCON, INTF
MAINLP
	CALL	NEXTSEG
	CALL	UPDATESEG
	CALL	TIME_L
	GOTO	MAINLP

; LOCKFLGについて
; 0 -> 左右をインクリメントする。
; 1 -> 右のみをインクリメントする。
; _ -> 何もしない。
NEXTSEG
	XORLF	LOCKFLG, 0H
	JZ		INCLEFT
	XORLF	LOCKFLG, 1H
	JZ		INCRIGHT
	RETURN
; SEGLをインクリメントする。
INCLEFT
	INCF	SEGL, F
	XORLF	SEGL, 0AH
	BTFSC	STATUS, 2
	CLRF	SEGL
; SEGRをインクリメントする。
INCRIGHT
	INCF	SEGR, F
	XORLF	SEGR, 0AH
	BTFSC	STATUS, 2
	CLRF	SEGR

	RETURN

; SEGL、SEGRに基づいて7セグへの出力を更新する。
UPDATESEG
	MOVF	SEGL, W
	MOVWF	PORTA

	MOVF	SEGR, W
	MOVWF	PORTB

	RETURN

;50us(250c*0.2us)
TIME50U						
	MOVLW	52H		; 1c 52H = 82		
	MOVWF	CNT1	; 1c
	NOP				; 1c
	NOP				; 1c
	NOP				; 1c
LOOP1
	DECFSZ	CNT1, F	; 1*(82-1)+2*1 = 83
	GOTO	LOOP1	; 2*(82-2) = 160
	RETURN			; 2c

TIME_S						
	MOVLW	7FH		; 1c 52H = 82		
	MOVWF	CNT2	; 1c
LOOP2
	DECFSZ	CNT2, F	; 1*(82-1)+2*1 = 83
	GOTO	LOOP2	; 2*(82-2) = 160
	RETURN			; 2c

TIME_M
	MOVLW	7FH 	; 1c 62H = 98
	MOVWF 	CNT3	; 1c
LOOP3
	CALL	TIME_S	; (2+250)*98 = 24696
	DECFSZ	CNT3, F	; 1*(98-1)+2*1 = 99
	GOTO	LOOP3	; 2*(98-1) = 194
	RETURN			; 2c

TIME_L
	MOVLW	18H 	; 1c 62H = 98
	MOVWF 	CNT4	; 1c
LOOP4
	CALL	TIME_M	; (2+250)*98 = 24696
	DECFSZ	CNT4, F	; 1*(98-1)+2*1 = 99
	GOTO	LOOP4	; 2*(98-1) = 194
	RETURN			; 2c

;出力を反転させる
TOGGLE
	MOVLW	b'10000000'
	XORWF	PORTB, F
	RETURN

;一定時間待つ
;args:
;	ARGPRE
;	ARGPOST
TIME_
	MOVF	ARGPRE, W
	MOVWF	CNT_
LOOP_PRE
	DECFSZ	CNT_, F
	GOTO	LOOP_PRE

	MOVF	ARGPOST, W
	MOVWF	CNT_
LOOP_POST
	CALL	TIME50U
	DECFSZ	CNT_, F
	GOTO	LOOP_POST
	RETURN

;SCALEからARGPREの値を返す
;args:
;	SCALE
PREFROMSCALE
	MOVF	SCALE, W
	ADDWF	PCL, F
	DT		74H, 92H, 28H, 54H
	DT		86H, 16H, 01H, 47H
	DT		3DH, 39H, 3AH, 3FH
	DT		49H, 02H, 14H, 2AH
	DT		43H, 0BH

;SCALEからARGPOSTの値を返す
;args:
;	SCALE
POSTFROMSCALE
	MOVF	SCALE, W
	ADDWF	PCL, F
	DT		20H, 1CH, 1AH, 18H
	DT		16H, 16H, 15H, 13H
	DT		12H, 11H, 10H, 0FH
	DT		0EH, 0EH, 0DH, 0CH
	DT		0BH, 0BH

;SCALEからCNT16の値を返す
;args:
;	SCALE
CNT16FROMSCALE
	MOVF	SCALE, W
	ADDWF	PCL, F
	DT		13H, 15H, 18H, 1AH
	DT		1BH, 1DH, 1FH, 20H
	DT		22H, 24H, 27H, 29H
	DT		2BH, 2EH, 31H, 34H
	DT		37H, 3AH

;5サイクル分音を鳴らす
;args:
;	ARGPRE
;	ARGPOST
PLAYx5
	MOVLW	5H
	MOVWF	CNTx5
LOOPx5
	CALL	TIME_
	CALL	TOGGLE
	DECFSZ	CNTx5, F
	GOTO	LOOPx5
	RETURN

;16分音符分音を鳴らす
;args:
;	SCALE
;	LENGTH
PLAY16
	CALL	PREFROMSCALE
	MOVWF	ARGPRE
	CALL	POSTFROMSCALE
	MOVWF	ARGPOST
	CALL	CNT16FROMSCALE
	MOVWF	ARG16

	MOVF	ARG16, W
	MOVWF	CNT16
LOOP16
	CALL	PLAYx5
	DECFSZ	CNT16, F
	GOTO	LOOP16
	RETURN

	END